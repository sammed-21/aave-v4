// SPDX-License-Identifier: LicenseRef-BUSL
pragma solidity ^0.8.0;

import 'forge-std/StdJson.sol';
import 'forge-std/Vm.sol';
import {console2 as console} from 'forge-std/console2.sol';

/// @title Logger
/// @author Aave Labs
/// @notice JSON output report and console logging for deployment scripts.
contract Logger {
  using stdJson for string;

  Vm private constant vm = Vm(address(bytes20(uint160(uint256(keccak256('hevm cheat code'))))));
  struct AddressEntry {
    string label;
    address value;
  }

  struct ValueEntry {
    string label;
    uint256 value;
  }

  string internal _outputPath;
  string internal _jsonKey;
  string internal _json;

  /// @dev Constructor.
  /// @param outputPath_ The directory path for JSON output files.
  constructor(string memory outputPath_) {
    _jsonKey = 'root';
    _outputPath = outputPath_;
    _json = _jsonKey;
  }

  /// @notice Writes a labelled address entry to the JSON report.
  /// @param label The key for the JSON entry.
  /// @param value The address value to record.
  function write(string memory label, address value) public {
    _write(label, value);
  }

  /// @notice Writes a labelled uint256 entry to the JSON report.
  /// @param label The key for the JSON entry.
  /// @param value The uint256 value to record.
  function write(string memory label, uint256 value) public {
    _write(label, value);
  }

  /// @notice Writes a plain string message to the JSON report.
  /// @param value The string message to record.
  function write(string memory value) public {
    _write(value);
  }

  /// @notice Writes a group of labelled address entries to the JSON report under a shared key.
  /// @param groupLabel The key for the JSON group.
  /// @param entries The array of address entries to record.
  function writeGroup(string memory groupLabel, AddressEntry[] memory entries) public {
    _writeGroup(groupLabel, entries);
  }

  /// @notice Writes a group of labelled uint256 entries to the JSON report under a shared key.
  /// @param groupLabel The key for the JSON group.
  /// @param entries The array of value entries to record.
  function writeGroup(string memory groupLabel, ValueEntry[] memory entries) public {
    _writeGroup(groupLabel, entries);
  }

  /// @notice Returns the accumulated JSON report string.
  function getJson() public view returns (string memory) {
    return _json;
  }

  /// @notice Persists the accumulated JSON report to a file on disk.
  /// @param fileName The base file name without extension.
  /// @param withTimestamp Whether to prepend a Unix timestamp to the file name.
  function save(string memory fileName, bool withTimestamp) public {
    console.log();
    console.log('Saving log to %s', _outputPath);
    string memory appendedMetadata = withTimestamp ? string.concat(_getTimestamp(), '-') : '';
    vm.writeJson(
      _json,
      string.concat(
        _outputPath,
        appendedMetadata,
        vm.toString(block.chainid),
        '-',
        fileName,
        '.json'
      )
    );
  }

  /// @notice Logs a labelled address to the console.
  /// @param label The label to display.
  /// @param value The address value to display.
  function log(string memory label, address value) public pure {
    _log(label, value);
  }

  /// @notice Logs a labelled uint256 to the console.
  /// @param label The label to display.
  /// @param value The uint256 value to display.
  function log(string memory label, uint256 value) public pure {
    _log(label, value);
  }

  /// @notice Logs a plain string message to the console.
  /// @param value The string message to display.
  function log(string memory value) public pure {
    _log(value);
  }

  /// @notice Logs a level-one header string to the console.
  /// @param value The header text to display.
  function logHeader1(string memory value) public pure {
    _logHeader1(value);
  }

  /// @notice Logs a level-one header with a labelled address to the console.
  /// @param label The header label to display.
  /// @param value The address value to display.
  function logHeader1(string memory label, address value) public pure {
    _logHeader1(label, value);
  }

  /// @notice Logs an indented detail line with a labelled address to the console.
  /// @param label The detail label to display.
  /// @param value The address value to display.
  function logDetail(string memory label, address value) public pure {
    _logDetail(label, value);
  }

  /// @notice Logs a blank line to the console.
  function logNewLine() public pure {
    _logNewLine();
  }

  function _write(string memory label, bytes32 value) internal {
    _json = vm.serializeBytes32(_jsonKey, label, value);
  }

  function _write(string memory label, address value) internal {
    _json = vm.serializeAddress(_jsonKey, label, value);
  }

  function _write(string memory label, uint256 value) internal {
    _json = vm.serializeUint(_jsonKey, label, value);
  }

  function _write(string memory value) internal {
    _json = vm.serializeString(_jsonKey, 'message', value);
  }

  function _writeGroup(string memory groupLabel, AddressEntry[] memory entries) internal {
    string memory group;
    for (uint256 i = 0; i < entries.length; i++) {
      group = vm.serializeAddress(groupLabel, entries[i].label, entries[i].value);
    }
    _json = vm.serializeString(_jsonKey, groupLabel, group);
  }

  function _writeGroup(string memory groupLabel, ValueEntry[] memory entries) internal {
    string memory group;
    for (uint256 i = 0; i < entries.length; i++) {
      group = vm.serializeString(groupLabel, entries[i].label, vm.toString(entries[i].value));
    }
    _json = vm.serializeString(_jsonKey, groupLabel, group);
  }

  function _getTimestamp() internal view returns (string memory) {
    return vm.toString(vm.unixTime() / 1000);
  }

  function _log(string memory label, address value) internal pure {
    console.log('%s: %s', label, value);
  }

  function _log(string memory label, uint256 value) internal pure {
    console.log('%s: %s', label, value);
  }

  function _log(string memory value) internal pure {
    console.log(value);
  }

  function _logHeader1(string memory value) internal pure {
    console.log('...%s...', value);
  }

  function _logHeader1(string memory label, address value) internal pure {
    console.log('...%s %s...', label, value);
  }

  function _logDetail(string memory label, address value) internal pure {
    console.log('  %s: %s', label, value);
  }

  function _logNewLine() internal pure {
    console.log();
  }
}
