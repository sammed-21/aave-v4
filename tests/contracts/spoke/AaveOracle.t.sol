// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import 'tests/setup/Base.t.sol';

/// forge-config: default.allow_internal_expect_revert = true
contract AaveOracleTest is Base {
  using SafeCast for uint256;

  AaveOracle public oracle;

  uint8 private constant _oracleDecimals = 8;

  address public deployer = makeAddr('DEPLOYER');

  address private _source1 = makeAddr('SOURCE1');
  address private _source2 = makeAddr('SOURCE2');

  address private user = makeAddr('USER');

  uint256 private constant reserveId1 = 0;
  uint256 private constant reserveId2 = 1;

  function setUp() public override {
    super.setUp();

    vm.startPrank(deployer);
    oracle = new AaveOracle(_oracleDecimals);
    spoke1 = ISpoke(
      address(
        AaveV4TestOrchestration.deploySpokeImplementation(
          address(oracle),
          MAX_ALLOWED_USER_RESERVES_LIMIT
        )
      )
    );
    oracle.setSpoke(address(spoke1));
    vm.stopPrank();
  }

  function test_constructor() public {
    vm.prank(deployer);
    oracle = new AaveOracle(_oracleDecimals);

    assertEq(oracle.spoke(), address(0));
    test_decimals();
  }

  function test_fuzz_constructor(uint8 decimals) public {
    decimals = bound(decimals, 0, 18).toUint8();
    oracle = new AaveOracle(decimals);

    assertEq(oracle.spoke(), address(0));
    assertEq(oracle.decimals(), decimals);
  }

  function test_decimals() public view {
    assertEq(oracle.decimals(), _oracleDecimals);
  }

  function test_setSpoke_revertsWith_OnlyDeployer(address setter) public {
    vm.assume(setter != deployer);

    vm.expectRevert(IAaveOracle.OnlyDeployer.selector);
    vm.prank(setter);
    oracle.setSpoke(address(spoke1));
  }

  function test_setSpoke_revertsWith_InvalidAddress() public {
    vm.expectRevert(IAaveOracle.InvalidAddress.selector);

    vm.prank(deployer);
    oracle.setSpoke(address(0));
  }

  function test_setSpoke_revertsWith_SpokeAlreadySet() public {
    vm.expectRevert(IAaveOracle.SpokeAlreadySet.selector);
    vm.prank(deployer);
    oracle.setSpoke(address(spoke1));
  }

  function test_setSpoke() public {
    vm.startPrank(deployer);
    oracle = new AaveOracle(_oracleDecimals);

    address newSpoke = address(
      AaveV4TestOrchestration.deploySpokeImplementation(
        address(oracle),
        MAX_ALLOWED_USER_RESERVES_LIMIT
      )
    );

    vm.expectEmit(address(oracle));
    emit IAaveOracle.SetSpoke(address(newSpoke));

    oracle.setSpoke(address(newSpoke));
    vm.stopPrank();

    assertEq(oracle.spoke(), address(newSpoke));
  }

  function test_setReserveSource_revertsWith_OnlySpoke() public {
    vm.expectRevert(IPriceOracle.OnlySpoke.selector);

    vm.prank(user);
    oracle.setReserveSource(reserveId1, address(0));
  }

  function test_setReserveSource_revertsWith_InvalidSourceDecimals() public {
    _mockSourceDecimals(_source1, _oracleDecimals + 1);

    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidSourceDecimals.selector, reserveId1));

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);
  }

  function test_setReserveSource_revertsWith_InvalidSource() public {
    _mockSourceDecimals(address(0), _oracleDecimals);

    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidSource.selector, reserveId1));

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, address(0));
  }

  function test_setReserveSource_revertsWith_InvalidPrice() public {
    _mockSourceDecimals(_source1, _oracleDecimals);
    _mockSourceLatestAnswer(_source1, -1e8);
    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidPrice.selector, reserveId1));
    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);

    _mockSourceLatestAnswer(_source1, 0);
    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidPrice.selector, reserveId1));
    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);

    _mockSourceLatestAnswer(_source1, -100e18);
    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidPrice.selector, reserveId1));
    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);
  }

  function test_setReserveSource_revertsWith_OracleMismatch() public {
    vm.startPrank(deployer);
    IAaveOracle newOracle = IAaveOracle(new AaveOracle(_oracleDecimals));

    // set new spoke to a separate oracle
    address mismatchOracle = address(new AaveOracle(_oracleDecimals));
    address newSpoke = address(
      AaveV4TestOrchestration.deploySpokeImplementation(
        mismatchOracle,
        MAX_ALLOWED_USER_RESERVES_LIMIT
      )
    );

    vm.expectRevert(IAaveOracle.OracleMismatch.selector);
    newOracle.setSpoke(newSpoke);
  }

  function test_setReserveSource() public {
    _mockSourceDecimals(_source1, _oracleDecimals);
    _mockSourceLatestAnswer(_source1, 1e8);

    vm.expectEmit();
    emit IAaveOracle.UpdateReserveSource(reserveId1, _source1);
    vm.expectCall(_source1, abi.encodeCall(IPriceFeed.latestAnswer, ()));

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);
  }

  function test_getReserveSource() public {
    assertEq(oracle.getReserveSource(reserveId1), address(0));
    test_setReserveSource();
    assertEq(oracle.getReserveSource(reserveId1), _source1);
  }

  function test_getReservePrice_revertsWith_InvalidSource() public {
    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidSource.selector, reserveId1));
    oracle.getReservePrice(reserveId1);
  }

  function test_getReservePrice_revertsWith_InvalidPrice() public {
    _mockSourceDecimals(_source1, _oracleDecimals);
    _mockSourceLatestAnswer(_source1, 1e8);

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);

    _mockSourceLatestAnswer(_source1, -1e8);

    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidPrice.selector, reserveId1));
    oracle.getReservePrice(reserveId1);
  }

  function test_getReservePrice() public {
    test_setReserveSource();

    vm.expectCall(_source1, abi.encodeCall(IPriceFeed.latestAnswer, ()));
    assertEq(oracle.getReservePrice(reserveId1), 1e8);
  }

  function test_getReservePrices_revertsWith_InvalidSource() public {
    _mockSourceDecimals(_source1, _oracleDecimals);
    _mockSourceLatestAnswer(_source1, 1e8);

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);

    uint256[] memory reserveIds = new uint256[](2); // todo: use reserveIds
    reserveIds[0] = reserveId1;
    reserveIds[1] = reserveId2;

    vm.expectRevert(abi.encodeWithSelector(IAaveOracle.InvalidSource.selector, reserveId2));
    oracle.getReservesPrices(reserveIds);
  }

  function test_getReservePrices() public {
    _mockSourceDecimals(_source1, _oracleDecimals);
    _mockSourceLatestAnswer(_source1, 1e8);
    _mockSourceDecimals(_source2, _oracleDecimals);
    _mockSourceLatestAnswer(_source2, 2e8);

    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId1, _source1);
    vm.prank(address(spoke1));
    oracle.setReserveSource(reserveId2, _source2);

    uint256[] memory reserveIds = new uint256[](2);
    reserveIds[0] = reserveId1;
    reserveIds[1] = reserveId2;

    uint256[] memory prices = oracle.getReservesPrices(reserveIds);
    assertEq(prices[0], 1e8);
    assertEq(prices[1], 2e8);
  }

  function _mockSourceDecimals(address source, uint8 decimals) internal {
    vm.mockCall(source, abi.encodeCall(IPriceFeed.decimals, ()), abi.encode(decimals));
  }

  function _mockSourceLatestAnswer(address source, int256 price) internal {
    vm.mockCall(source, abi.encodeCall(IPriceFeed.latestAnswer, ()), abi.encode(price));
  }
}
